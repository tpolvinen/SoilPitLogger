'CR800 Series Datalogger

'For programming tips, copy this address to your browser
'search window:https://www.campbellsci.com/videos/datalogger-programming
'To create a different opening program template, type in new
'instructions and select Template | Save as Default Template
'date: 2022-06-15
'program author: Tatu Polvinen, UH, Env.Soil Sci.

'*** Contstant Table 
ConstTable (Constants) 
'* Measurement constants 
Const OUTPUT_INTERVAL = 30           'Online mean output interval in minutes.
Const CAL_INTERVAL    = 1440         'HFP01SC insitu calibration interval (minutes).
Const END_CAL = OUTPUT_INTERVAL - 1  'End HFP01SC insitu calibration one minute before the next Output.

'Enter sensor sensitivities and heater resistance values as constants. 
'These values are unique for each sensor and are listed in the product certificate  
Const HFP01SC_SNSTVT_1 = 60.80 'Change to your HFP01SC #1 sensitivity (uV/(W/m^2).
Const HFP01SC_RSSTNC_1 = 96.5  'Change to your HFP01SC #1 heater resistance (ohm).
Const HEAT_REF_RSSTNC  = 10.0  'Resistance of heater reference resister (ohm).
Const HFP01SC_AREA = 3.855e-3  'Area of HFP01SC (m^2).
EndConstTable

'*** Wiring instruction 
' * Plate cable 
Const PLATE_ANALOG_INPUT = 1    'Starting differential analog input channel in CR1000X for plate measurements.

' CR800 1H     HFP signal #1 (white)
' CR800 1L     HFP signal reference #1 (green)
' CR800 AG     HFP Shield #1 (clear)
  
' * Heater cable 
Const HEATER_ANALOG_INPUT = 2   'Starting differential analog input channel in CR1000X for heater measurements.

' CR800 2H     Heater resistor signal #1 (yellow)
' CR800 2L     Heater resistor signal reference #1 (purple)
' CR800 SW12   Heater power positive #1 (red)
' CR800 G      Heater power negative #1 (black)
' CR800 AG     Heater shield #1 (clear)

'*** Variables
' * Used for measurements
Public shf_plate(3)
Units  shf_plate = W/m^2
Public shf_multiplier(1) = {1000/HFP01SC_SNSTVT_1} 'Hold multipliers used in computation
Units  shf_multiplier    = W/(m^2 mV)
Dim    shf_mfct_mult(1)  = {1000/HFP01SC_SNSTVT_1} 'Hold manufacture multipliers all time
Dim    shf_htr_resstnc(1)= {HFP01SC_RSSTNC_1}                'Array to load heater resistance in ohm

' * Used for self-calibration
Dim shf_mV(3)                      'Voltage measured from soil heat flux plates
Dim shf_mV_run(3)                  'Running mean of shf_mV
Dim shf_mV_0(3)                    'Running mean of shf_mV at the beginning of calibration
Dim shf_mV_170(3)                  'Running mean of shf_mV after 170 seconds since the beginning of calibration
Dim shf_mV_180(3)                  'Running mean of shf_mV after 180 seconds since the beginning of calibration
Dim shf_mV_end(3)                  'Running mean of shf_mV at the end of calibration
Dim V_rf(3)                        'Reference voltage
Dim V_rf_run(3)                    'Running mean of reference voltage
Dim V_rf_180(3)                    'Running mean of reference voltage after 180 seconds since the beginning of calibration
Dim shf_cal_on_f As Boolean        'Set true while calibration is on
Public shf_wrnng_flg(3) As Boolean 'HFP01SC self-calibration warning   

Dim i As Long
Dim truefalse (2) = {"True", "False"} As String*5

DataTable (shf_mean, TRUE, 100)
  DataInterval (0, OUTPUT_INTERVAL, Min, 10)

  Average (3, shf_plate(1),      IEEE4, 0)   'Soil heat flux through a plate  

  Sample (1, shf_multiplier(1),  IEEE4)      'Multipliers for soil heat flux plates
  Sample (1, shf_htr_resstnc(1), IEEE4)      'Heater resistance of self-calibrated soil heat flux plate
      
  'Warning flag indicates the peformace of self-calibration #1 (see HFP01SC manual v1624) 
  Sample (1, truefalse (2 + shf_wrnng_flg(1)), String)   
  FieldNames ("shfp_wrnng_1_1_1")
  Sample (1, truefalse (2 + shf_wrnng_flg(2)), String)    
  FieldNames ("shfp_wrnng_2_1_1")
  Sample (1, truefalse (2 + shf_wrnng_flg(3)), String)    
  FieldNames ("shfp_wrnng_3_1_1")

EndTable

'Define Subroutines
'Sub
	'EnterSub instructions here
'EndSub

BeginProg

  Scan (1, Sec, 3, 0)

    '*** HFP01SC measurements 
    VoltDiff (shf_mV(1), 1, mV250, PLATE_ANALOG_INPUT, TRUE, 500, 60, 1, 0)
    
    'Apply HFP01SC soil heat flux plate calibration.
      shf_plate(1) = shf_mV(1)*shf_multiplier(1)
    
    '*** Measure voltage across the heater (V_Rf).
    VoltDiff (V_rf(1), 1, mV5000, HEATER_ANALOG_INPUT, TRUE, 500, 60, 0.001, 0)

    'Maintain filtered values for calibration.
    AvgRun (shf_mV_run(1), 1, shf_mV(1), 5)
    AvgRun (V_rf_run(1),   1, V_rf(1),   5)

    ' *** HFP01SC self-calibration one minute into CAL_INTERVAL.
    If (TimeIntoInterval (1,CAL_INTERVAL,Min)) Then
      shf_cal_on_f = TRUE
      Move (shf_mV_0(1), 1, shf_mV_run(1), 1)
      
    'Power the HFP01SC heaters.
     SW12(1)
    EndIf

    ' *** Record voltage output 10 seconds before end of heating cycle.
    If (TimeIntoInterval (230, CAL_INTERVAL*60, Sec)) Then
      Move (shf_mV_170(1), 1, shf_mV_run(1), 1)
    EndIf
    ' *** Record voltage output at end of heating cycle and power down heaters.
    If (TimeIntoInterval (4,CAL_INTERVAL,Min)) Then
      Move (shf_mV_180(1), 1, shf_mV_run(1), 1)
      Move (V_rf_180(1),   1, V_rf_run(1),   1)
    
      SW12(0)
    EndIf

    'End HFP01SC calibration sequence.
     If (TimeIntoInterval (END_CAL,CAL_INTERVAL,Min)) Then
      Move (shf_mV_end(1), 1, shf_mV_run(1), 1)
      'Compute new HFP01SC calibration factors.
      i = 1
        If (V_rf_180(i) <> NaN) AND (shf_mV_0(i) <> NaN) AND (shf_mV_180(i) <> NaN) Then
           shf_wrnng_flg(i) = FALSE
           
          'In the following equation 2 means that heat emitts in two directions 
          
          shf_multiplier(i) = ((V_rf_180(i)*V_rf_180(i)*shf_htr_resstnc (i)/2/HFP01SC_AREA/HEAT_REF_RSSTNC/HEAT_REF_RSSTNC) _
                                 /ABS (shf_mV_180(i)-((shf_mV_0(i)+shf_mV_end(i))/2)))
        
          If ((1000/shf_multiplier(i) < 800/shf_mfct_mult(i)) OR (1000/shf_multiplier(i) > 1050/shf_mfct_mult(i)) _
                OR (ABS(shf_mV_end(i)-shf_mV_0(i)) > 0.1* ABS(shf_mV_180(i)-((shf_mV_0(i)+shf_mV_end(i))/2))) _
                OR (ABS(shf_mV_180(i)-shf_mV_170(i)) > 0.1 *ABS(shf_mV_180(i)-((shf_mV_0(i)+shf_mV_end(i))/2)))) Then 
             shf_wrnng_flg(i) = TRUE                             
             shf_multiplier(i) = shf_mfct_mult(i) 
          EndIf
                            
        Else 
            shf_wrnng_flg(i) = TRUE 
        EndIf

        shf_cal_on_f = FALSE
   EndIf
    
     CallTable shf_mean
 
  NextScan
EndProg

